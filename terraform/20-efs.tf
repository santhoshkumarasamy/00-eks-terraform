resource "aws_efs_file_system" "eks" {
  creation_token = "efs"

  performance_mode = "generalPurpose"
  throughput_mode  = "bursting"
  encrypted        = true

}


resource "aws_efs_mount_target" "zone_a" {
  file_system_id  = aws_efs_file_system.eks.id
  subnet_id       = aws_subnet.private_zone1.id
  security_groups = [aws_eks_cluster.eks.vpc_config[0].cluster_security_group_id]
}

resource "aws_efs_mount_target" "zone_b" {
  file_system_id  = aws_efs_file_system.eks.id
  subnet_id       = aws_subnet.private_zone2.id
  security_groups = [aws_eks_cluster.eks.vpc_config[0].cluster_security_group_id]
}

data "aws_iam_policy_document" "efs_csi_driver" {
  statement {
    actions = ["sts:AssumeRoleWithWebIdentity"]
    effect  = "Allow"

    condition {
      test     = "StringEquals"
      variable = "${replace(aws_iam_openid_connect_provider.eks.url, "https://", "")}:sub"
      values   = ["system:serviceaccount:kube-system:efs-csi-controller-sa"]
    }

    principals {
      identifiers = [aws_iam_openid_connect_provider.eks.arn]
      type        = "Federated"
    }
  }
}


resource "aws_iam_role" "efs-csi-driver" {
  name               = "my-${aws_eks_cluster.eks.name}-efs-csi-driver"
  assume_role_policy = data.aws_iam_policy_document.efs_csi_driver.json
}

resource "aws_iam_role_policy_attachment" "efs-csi-driver" {
  policy_arn = "arn:aws:iam::aws:policy/service-role/AmazonEFSCSIDriverPolicy"
  role       = aws_iam_role.efs-csi-driver.name
}


resource "helm_release" "efs-csi-driver" {
  name = "aws-efs-csi-driver"

  version    = "3.2.4"
  repository = "https://kubernetes-sigs.github.io/aws-efs-csi-driver"
  namespace  = "kube-system"
  chart      = "aws-efs-csi-driver"

  set = [
    {
      name  = "controller.serviceAccount.name"
      value = "efs-csi-controller-sa"
    },
    {
      name  = "controller.serviceAccount.annotations.eks\\.amazonaws\\.com/role-arn"
      value = aws_iam_role.efs-csi-driver.arn
    }
  ]

  depends_on = [
    aws_efs_mount_target.zone_a,
    aws_efs_mount_target.zone_b
   ]
}


data "aws_eks_cluster" "eks_v2" {
  name=aws_eks_cluster.eks.name
}

data "aws_eks_cluster_auth" "eks_auth_v2" {
  name = aws_eks_cluster.eks.name
}

provider "kubernetes" {
  host = data.aws_eks_cluster.eks_v2.endpoint
  cluster_ca_certificate = base64decode(data.aws_eks_cluster.eks_v2.certificate_authority[0].data)
  token = data.aws_eks_cluster_auth.eks_auth_v2.token
}

resource "kubernetes_storage_class_v1" "efs" {
  metadata {
    name = "efs"
  }

  storage_provisioner = "efs.csi.aws.com"

  parameters = {
    provisioningMode ="efs-ap"
    fileSystemId = aws_efs_file_system.eks.id
    directoryPerms = "700"
  }

  mount_options = [ "iam" ]

  depends_on = [ helm_release.efs-csi-driver ]
}